import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.70'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "kotlin"

group = 'com.mineinabyss'
version = plugin_version

if (project.hasProperty("buildNo")) version += ".$buildNo"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def github_username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
def github_password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

repositories {
    mavenCentral()
    jcenter()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven {
        name = "MineInAbyss"
        url = uri("https://maven.pkg.github.com/MineInAbyss/Idofront")
        credentials {
            username = github_username
            password = github_password
        }
    }
    maven {
        name = "MineInAbyss"
        url = uri("https://maven.pkg.github.com/MineInAbyss/Guiy")
        credentials {
            username = github_username
            password = github_password
        }
    }
    maven {
        name = "CultOfClang"
        url = uri("https://maven.pkg.github.com/CultOfClang/Minecraft/")
        credentials {
            username = github_username
            password = github_password
        }
    }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://erethon.de/repo/' } //HeadLib
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://jitpack.io' } //JitPack
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url 'https://maven.sk89q.com/repo/' } //WorldGuard/Edit
    mavenLocal()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = "Mobzy"
            url = uri("https://maven.pkg.github.com/MineInAbyss/Mobzy")
            credentials {
                username = github_username
                password = github_password
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version" //Spigot
    compileOnly "org.spigotmc:spigot:$server_version" // NMS
    compileOnly "de.erethon:headlib:3.0.2"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.2" //WorldGuard
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"
    compileOnly "com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721"
    compileOnly "org.nield:kotlin-statistics:1.2.1"
    compileOnly "org.cultofclang.minecraft:kotlinspice:0.1.12"

    if (useLocalGuiy.toBoolean()) implementation project(':guiy')
    else implementation "com.derongan.minecraft:guiy:0.1.+"

    if (useLocalIdofront.toBoolean()) implementation project(':idofront')
    else implementation "com.mineinabyss:idofront:0.1.+"
}

shadowJar {
    getArchiveBaseName().set("Mobzy")
    getArchiveClassifier().set(null)

    relocate 'com.derongan.minecraft.guiy', "${project.group}.${project.name}.guiy".toLowerCase()
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize {
        exclude(dependency('de.erethon:headlib:3.0.2'))
        exclude(dependency('com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721'))
    }
}
build.dependsOn shadowJar

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [plugin_version: plugin_version]
    }
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
}